<%args>
	$tourn
	$perms
	$tourn_settings
	$date       => undef
	$target_day => undef
</%args>
<%init>

	my $tz = $tourn->tz;
	$tz = "UTC" unless $tz;

	my $today = eval {
		my $dt = Tab::dateme($date);
		$dt->set_time_zone($tz);
		return $dt;
	};

	my $target = eval {
		my $dt = Tab::dateme($target_day);
		$dt->set_time_zone($tz);
		return $dt;
	};

	Tab::debuglog("Today is $today from $date");
	Tab::debuglog("Target is $target from $target_day");

	my $msg;
	my $url;

	if ($today && $target) {

		my $counter;

		foreach my $timeslot ($tourn->timeslots) {

			my $start = $timeslot->start;
			$start->set_time_zone("UTC");
			$start->set_time_zone($tz);

			next if $start->mdy ne $today->mdy;
			next if $start->mdy eq $target->mdy;

			my $end = $timeslot->end;
			$end->set_time_zone("UTC");
			$end->set_time_zone($tz);

			my $round_duration = $end - $start;

			$start->set(
				year  => $target->year,
				month => $target->month,
				day   => $target->day,
			);

			my $new_end = $start->clone() + $round_duration;

			$timeslot->start($start);
			$timeslot->end($new_end);
			$timeslot->update();

			$counter++;
		}

		$msg = "Moved ".$counter." timeslots to ".$target->day_name." the ".$target->day;
		$url = $target->mdy('/');

	} else {

		$msg = "No timeslots found to move";
	}

	unless ($url) {
		if ($today) {
			$url = $today->mdy('/');
		}
	}

	$m->redirect("sked.mhtml?msg=$msg&date=".$url);

</%init>
