<%args>
	$category
	$school
</%args>
<%init>

	my %burdens;

    my $dbh = Tab::DBI->db_Main(); 

    my $sth = $dbh->prepare("
		select distinct entry.id, 
			event.id as eventid,
			burden.value burden,
			nats_jpool.value jpool,
			nats_screwy_burden.value nats_screwy_burden,
			hire.value as hire,
			hire_price.value as hire_price

			from (entry, event, 	
				event_setting burden, 
				event_setting nats_jpool)

			left join event_setting nats_screwy_burden
				on nats_screwy_burden.event = event.id
				and nats_screwy_burden.tag = 'nats_screwy_burden'

			left join jpool_setting hire 
				on hire.jpool = nats_jpool.value
				and hire.tag = 'hire'

			left join jpool_setting hire_price 
				on hire_price.jpool = nats_jpool.value
				and hire_price.tag = 'hire_price'

		where entry.school = ?
		and entry.unconfirmed = 0
		and entry.event = event.id
		and event.category = ?

		and burden.event = event.id
		and burden.tag = 'nats_judge_burden'

		and nats_jpool.event = event.id
		and nats_jpool.tag = 'nats_jpool'

		and not exists (
			select id from event_setting
			where event_setting.event = event.id
			and event_setting.tag = 'no_judge_burden'
			and event_setting.value = 1
		)

		and not exists (
			select id from entry_setting
			where entry_setting.entry = entry.id
			and entry_setting.tag = 'rejected_by'
		)

	");

	$sth->execute($school->id, $category->id);

	my %events;

	while( 

		my( $entry_id, $event_id, $burden, $jpool_id,
			$nats_screwy_burden, $hire, $hire_price
		) = $sth->fetchrow_array()

	){ 

		$events{$event_id}{"count"}++;
		$events{$event_id}{"jpool"} = $jpool_id;
		$events{$event_id}{"burden"} = $burden;
		$events{$event_id}{"screwy_burden"}++ if $nats_screwy_burden;
		$events{$event_id}{"hire"} = $hire;
		$events{$event_id}{"hire_price"} = $hire_price;
	}

	foreach my $event_id (keys %events) { 

		my $burden;

		# This is some serious pissant bullshit right here. 

		if ($events{$event_id}{"screwy_burden"}) { 

			my $full = $events{$event_id}{"burden"};
			my $half = POSIX::floor($full / 2);

			foreach my $count (1 .. $events{$event_id}{"count"}) { 

				if ($count % 2) { 
					#odds get full
					$burden += $full;
				} else { 
					$burden += $half;
				}
			}

		} else { 

			$burden += ($events{$event_id}{"count"} * $events{$event_id}{"burden"});

		}

		$burdens{$events{$event_id}{"jpool"}} += $burden;
		$burdens{"total"} += $burden;

		$burdens{"hire"}{$events{$event_id}{"jpool"}} = $events{$event_id}{"hire"};
		$burdens{"hire_price"}{$events{$event_id}{"jpool"}} = $events{$event_id}{"hire_price"};

	}

	my $min_burden = $category->setting("min_burden");
	my $max_burden = $category->setting("max_burden");

	if ($burdens{"total"} > 0 
		&& $min_burden > 0
		&& $burdens{"total"} < $min_burden
	) { 
		$burdens{"total"} = $min_burden;
	}

	if ( $max_burden 
		&& $burdens{"total"} > $max_burden 
	)  { 
		$burdens{"total"} = $max_burden;
	}

	return %burdens;	

</%init>
