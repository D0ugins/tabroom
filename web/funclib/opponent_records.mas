<%args>
	$event_id
	$round_name => undef
</%args>
<%init>

	my $dbh = Tab::DBI->db_Main();

	my $limit;

	if (int($round_name) > 0) {
		$limit = " and round.name <= ".int($round_name);
	}

	my $sth = $dbh->prepare("
		select
			score.value,
			ballot.side, ballot.entry, ballot.judge,
			panel.id, panel.letter,
			round.id, round.name, round.type

		from (round, panel, ballot, score)

		where round.event = ?
			and round.id = panel.round
			and round.type in ('highhigh', 'highlow', 'prelim')
			and panel.bye != 1
			and panel.id = ballot.panel
			and ballot.bye != 1
			and ballot.forfeit != 1
			and ballot.id = score.ballot
			and score.tag = 'winloss'
			$limit
	");

	$sth->execute($event_id);

	my %results;

	while (
		my (
			$win,
			$side, $entry, $judge,
			$panel, $letter,
			$round, $name, $type
		) = $sth->fetchrow_array()
	) {

		next if $results{"panels"}{$panel}{$entry}{"judge"}{$judge}++;
		$results{"entries"}{$entry}{"panels"}{$panel}++;
		$results{"panels"}{$panel}{$entry}{"wincount"} += $win;
	}

	my %opponents;

	foreach my $entry (
		keys %{$results{"entries"}}
	) {

		foreach my $panel (
			keys %{$results{"entries"}{$entry}{"panels"}}
		) {

			my $opponent;
			my $result;
			my $entry_wins = $results{"panels"}{$panel}{$entry}{"wincount"};

			foreach my $other (
				keys %{$results{"panels"}{$panel}}
			) {

				next if $other == $entry;

				$opponent = $other;
				my $other_wins = $results{"panels"}{$panel}{$other}{"wincount"};

				if ( $entry_wins > $other_wins ) {
					$result = "win";

				} elsif ( $entry_wins < $other_wins ) {
					$result = "loss";

				} elsif ( $entry_wins == $other_wins ) {
					$result = "split";
				}
			}

			$opponents{$entry}{"by_entry"}{$opponent} = $result;
			$opponents{$entry}{"by_result"}{$result}{$opponent}++;
		}
	}

	return %opponents;

</%init>

