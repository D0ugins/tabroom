<%args>
	$category
</%args>
<%init>

	return unless $category;

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("
		select round.id, round.name,
			timeslot.name,
			timeslot.id, timeslot.start, timeslot.end,
			num_judges.value, count(panel.id),
			event.abbr

		from (round, event, timeslot, panel)
			left join round_setting num_judges
				on num_judges.round = round.id
				and num_judges.tag='num_judges'

		where event.category = ?
		and round.event = event.id
		and round.timeslot = timeslot.id
		and round.id = panel.round
		and panel.bye != 1
		group by round.id
	");

	$sth->execute($category->id);

	my %timeslot;
	my %time_cache;

	while (
		my (
			$round_id, $round_name,
			$timeslot_name,
			$timeslot_id, $timeslot_start, $timeslot_end,
			$num_judges, $section_count,
			$event_abbr
		) = $sth->fetchrow_array()
	) {

		unless ($time_cache{$timeslot_start}) {
			my $dt = DateTime::Format::MySQL->parse_datetime($timeslot_start);
			$time_cache{$timeslot_start} = $dt->epoch;
		}

		unless ($time_cache{$timeslot_end}) {
			my $dt = DateTime::Format::MySQL->parse_datetime($timeslot_end);
			$time_cache{$timeslot_end} = $dt->epoch;
		}

		$timeslot{$timeslot_id}{"name"} = $timeslot_name;
		$timeslot{$timeslot_id}{"start"} = $time_cache{$timeslot_start};
		$timeslot{$timeslot_id}{"end"} = $time_cache{$timeslot_end};

		$timeslot{$timeslot_id}{"sections"} += $section_count;
		$num_judges = 1 unless $num_judges;
		$timeslot{$timeslot_id}{"judges"} += ($section_count * $num_judges);
		$timeslot{$timeslot_id}{"rounds"}++;
	}

	my $max_timeslot;
	my $max_judges;
	my $max_sections;

	foreach my $timeslot_id (keys %timeslot) {

		my $judge_count = $timeslot{$timeslot_id}{"judges"};
		my $section_count = $timeslot{$timeslot_id}{"sections"};
		my $round_count = $timeslot{$timeslot_id}{"rounds"};

		if ($judge_count > $max_judges) {
			$max_judges = $judge_count;
			$max_sections = $section_count;
			$max_timeslot = $timeslot_id;
		}
	}

	return $max_judges, $max_sections, $max_timeslot, $timeslot{$max_timeslot}{"name"};

</%init>

