<%args>	
	$category
</%args>
<%init>

	return unless $category;

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("
		select judge.id, judge.first, judge.last, judge.active,
			judge.school, judgeschool.name,
			judge.obligation, judge.hired,
			strike.id, strike.type, 
			entry.id, entry.code, 
			school.id, school.code, school.name, strike.registrant
		from (judge, strike)

		left join entry on strike.entry = entry.id
		left join school on strike.school = school.id
		left join school judgeschool on judge.school = judgeschool.id

		where judge.category = ? 
		and judge.id = strike.judge
	");

	$sth->execute($category->id);

	my %strikes;

	while (
		my (
			$judge_id, $judge_first, $judge_last, $judge_active,
			$judge_school, $judge_schoolname,
			$judge_obligation, $judge_hired,
			$strike_id, $strike_type, 
			$entry_id, $entry_code, 
			$school_id, $school_code, $school_name, 
			$strike_registrant
		)  = $sth->fetchrow_array()
	) {

		$strike_type = "Tab ".$strike_type unless $strike_registrant;
		$strikes{$judge_id}{"name"} = $judge_first." ".$judge_last;
		$strikes{$judge_id}{"last"} = $judge_last;
		$strikes{$judge_id}{"active"} = $judge_active;
		$strikes{$judge_id}{"obligation"} = $judge_obligation + $judge_hired;

		next if $school_id == $judge_school;

		if ($judge_school) { 
			$strikes{$judge_id}{"schools"}{$judge_school}{"Own school"} = $judge_schoolname;
		}

		if ($entry_id) { 
			$strikes{$judge_id}{"entries"}{$entry_id}{$strike_type} = $entry_code;
		} elsif ($school_id) { 
			$strikes{$judge_id}{"schools"}{$school_id}{$strike_type} = $school_name;
		}

	}

	return %strikes;

</%init>
