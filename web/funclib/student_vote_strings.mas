<%args>
	$round
	$entries_ref  => undef
</%args>
<%init>

	unless ($round && $round->id) {
		return;
	}

	my $event = $round->event;
	my $type = $event->type;

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("
		select
			vote.id, vote.tag, vote.value, vote.entry, vote.voter,
			panel.id, panel.letter, round.id, round.name
		from (student_vote vote, panel, round)
		where vote.panel = panel.id
		and panel.round = round.id
		and round.name <= ?
		and round.event =  ?
		and not exists (
			select round_setting.id
				from round_setting where round_setting.tag = 'ignore_results'
				and round_setting.round = round.id
			)
		order by round.name, vote.entry, vote.voter
	");

	$sth->execute($round->name, $event->id);

	my %rounds;
	my %entries;
	my %done;

	while (
		my (
			$vote_id, $vote_tag, $vote_value, $entry_id, $vote_voter,
			$panel_id, $panel_letter, $round_id, $round_name
		) = $sth->fetchrow_array()
	) {

		next if $done{$vote_id}++;
		next if $vote_tag eq "nominee";
		next unless $vote_value;

		$entries{$entry_id}{$round_id}{"student_vote"}{$vote_voter} = $vote_value;

		$rounds{$round_id} = $round_name;

		$entries{$entry_id}{$round_id}{"total"}{"student_vote"} = 0
			unless $entries{$entry_id}{$round_id}{"total"}{"student_vote"};

		$entries{$entry_id}{$round_id}{"total"}{"student_vote"} += $vote_value;
	}

	my %entry_ballots;

	foreach my $entry_id (keys %entries) {

		foreach my $round_id (
			sort {$rounds{$a} <=> $rounds{$b}}
			keys %{$entries{$entry_id}}
		) {

			$entry_ballots{$entry_id} .= "\n";
			$entry_ballots{$entry_id} .= "R".$rounds{$round_id};

			foreach (length($rounds{$round_id}) .. 2) {
				$entry_ballots{$entry_id} .= " ";
			}

			foreach my $voter_id (sort keys %{$entries{$entry_id}{$round_id}{'student_vote'}}) {

				if ($entries{$entry_id}{$round_id}{"student_vote"}{$voter_id}) {
					$entry_ballots{$entry_id} .= " ".$entries{$entry_id}{$round_id}{"student_vote"}{$voter_id};
				}
			}

			if ($entries{$entry_id}{$round_id}{"total"}{"student_vote"}) {
				$entry_ballots{$entry_id} .= " (";
				$entry_ballots{$entry_id} .= $entries{$entry_id}{$round_id}{"total"}{"student_vote"};
				$entry_ballots{$entry_id} .= ") ";
			}
		}
	}

	return %entry_ballots;

</%init>
