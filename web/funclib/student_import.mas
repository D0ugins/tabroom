<%args>
	$nsda_student
	$chapter
</%args>
<%init>

	my $graduated = 0;
	$graduated++ if $nsda_student->graduated;

	my $ualt_id = $nsda_student->ualt_id;

	my $student = Tab::Student->search(
		ualt_id   => $ualt_id,
		chapter   => $chapter->id
	)->first;

	return if $student;

	$student = Tab::Student->search(
		first     => $nsda_student->ufname,
		last      => $nsda_student->ulname,
		chapter   => $chapter->id
	)->first;

	if ($student) { 

		$student->ualt_id($ualt_id);
		$student->update();

	} else { 

		$student = Tab::Student->create({
			first     => $nsda_student->ufname,
			middle    => $nsda_student->umname,
			last      => $nsda_student->ulname,
			grad_year => $nsda_student->grad_yr,
			novice    => 0,
			retired   => $graduated,
			ualt_id   => $ualt_id,
			nsda      => $nsda_student->user_id,
			chapter   => $chapter->id
		});
	}

	my $level = $chapter->level;

	Tab::StudentSetting->create({
		student => $student->id,
		tag     => 'nsda_points',
		value   => $nsda_student->total_pts
	});

	Tab::StudentSetting->create({
		student => $student->id,
		tag     => 'nsda_paid',
		value   => $nsda_student->paid_status
	});

	if ($level eq "highschool") { 

		Tab::StudentSetting->create({
			student => $student->id,
			tag     => 'nsda_joined',
			value   => $nsda_student->high_joined
		});

	} elsif ($level eq "middle") { 

		Tab::StudentSetting->create({
			student => $student->id,
			tag     => 'nsda_joined',
			value   => $nsda_student->middle_joined
		});

	}

	if ($ualt_id) { 

		Tab::Student->set_sql( person_update => "
			update student, person
				set student.person = person.id
				where student.id = ? 
				and person.ualt_id = ? 
		");

		Tab::Student->sql_person_update->execute($student->id, $ualt_id);

	}

	return $student;

</%init>
