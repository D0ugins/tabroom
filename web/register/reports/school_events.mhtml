<%args>
	$tourn
	$tourn_settings
</%args>
<%init>

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("
		select
			entry.id,
			event.id, event.abbr, event.type,
			school.id, school.name

		from (entry, school, event)

		where school.tourn = ?
			and school.id = entry.school
			and entry.active = 1
			and entry.event = event.id

		group by entry.id
	");

	$sth->execute($tourn->id);

	my %counts;
	my %meta;

	while (
		my (
			$entry_id,
			$event_id, $event_abbr, $event_type,
			$school_id, $school_name
		) = $sth->fetchrow_array()
	) {

		$counts{$school_id}{$event_id}++;

		$meta{"event"}{$event_id}      = $event_abbr;
		$meta{"event_type"}{$event_id} = $event_type;
		$meta{"school"}{$school_id}    = $school_name;
	}

</%init>

	<div class="blankfull">

		<& "/funclib/tablesorter.mas", table => "matrix" &>

		<span class="fivesixths">
			<h4>School/event matrix</h4>
		</span>
		<span
			class = "sixth rightalign"
			id    = "matrix_buttonarea"
		>
		</span>

		<table id="matrix">

			<thead>
				<tr class="yellowrow">
					<th>
						School
					</th>
<%perl>
					foreach my $event_id (
						sort {
							$meta{"event_type"}{$a} cmp $meta{"event_type"}{$b}
							|| $meta{"event"}{$a} cmp $meta{"event"}{$b}
						} keys %{$meta{"event"}}
					) {
</%perl>
						<th class="centeralign">
							<% $meta{"event"}{$event_id} %>
						</th>
%					}

					<th class="centeralign redtext">
						Total
					</th>
				</tr>
			</thead>

			<tbody>
<%perl>
				foreach my $school_id (
					sort {
						$meta{"school"}{$a} cmp $meta{"school"}{$b}
					} keys %{$meta{"school"}}
				) {
</%perl>
					<tr>

						<td class="semibold bluetext">
							<& "/funclib/short_name.mas", name => $meta{"school"}{$school_id}, printme => 1 &>
						</td>
<%perl>
						my $total;

						foreach my $event_id (
							sort {
								$meta{"event_type"}{$a} cmp $meta{"event_type"}{$b}
								|| $meta{"event"}{$a} cmp $meta{"event"}{$b}
							} keys %{$meta{"event"}}
						) {

							$total += $counts{$school_id}{$event_id};
</%perl>
							<td class="centeralign">
								<% $counts{$school_id}{$event_id} %>
							</td>
%						}

						<td class="centeralign bluetext semibold">
							<% $total %>
						</td>
					</tr>
%				}

			</tbody>
		</table>
	</div>
