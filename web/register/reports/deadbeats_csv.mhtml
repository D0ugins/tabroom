<%args>
	$tourn
	$tourn_settings
</%args>
<%init>

    my $name = $tourn->name;

	my %category_settings;
	my %event_settings;

	my @categories = $tourn->categories();
	my @events = $tourn->events();
	my @concessions = $tourn->concessions();

	foreach my $category (@categories) { 
		%{$category_settings{$category->id}} = $category->all_settings();
		$category_settings{$category->id}{"shifts"} = $category->shifts(type => "strike");
	}

	foreach my $event (@events) { 
		%{$event_settings{$event->id}} = $event->all_settings();
		$event_settings{$event->id}{"fee"} = $event->fee;
	}

	my @entries = $m->comp(
		"/funclib/tourn_entries.mas", 
			tourn => $tourn,
			all   => 1
	);

	my %school_entries;

	foreach my $entry (@entries) { 
		push @{$school_entries{$entry->schoolid}}, $entry;
	}

	my @judges = $m->comp(
		"/funclib/tourn_judges.mas", 
			tourn => $tourn,
			all   => 1
	);

	my %school_judges;

	foreach my $judge (@judges) { 
		push @{$school_judges{$judge->schoolid}{$judge->categoryid}}, $judge;
		push @{$school_judges{$judge->schoolid}{"all"}}, $judge;
	}

	my @fines = $m->comp(
		"/funclib/tourn_fines.mas", 
			tourn => $tourn,
			all   => 1
	);

	my %school_fines;

	foreach my $fine (@fines) { 
		push @{$school_fines{$fine->schoolid}}, $fine;
	}

	my @hotels = $tourn->hotels;

	if (@hotels) { 
		%{$tourn_settings->{$tourn->id}{"hotels"}} =
			map {$_->id => $_} @hotels;
	}

	my @hires = $m->comp(
		"/funclib/tourn_hires.mas",
		tourn => $tourn 
	);

	my %school_hires;
	foreach my $hire (@hires) { 
		push @{$school_hires{$hire->schoolid}{$hire->categoryid}}, $hire;
		push @{$school_hires{$hire->schoolid}{"all"}}, $hire;
	}



    $name =~ s/[\W_]//g;
    my $filename = "NonZeroBalances-$name.csv";

	$m->clear_buffer;
    $r->content_type('application/csv');
    $r->headers_out->{'Content-Disposition'} = "attachment; filename=$filename";

	$m->print("School, Balance, Onsite Contact, Contact Number, ");
	$m->print("Contact Email, Refund Payable, Refund Address \n");

	foreach my $school (sort {$a->name cmp $b->name} $tourn->schools) {

		my ($total, $feline_ref, $total_ref)  =
			$m->comp("/funclib/school_fees.mas", 
				school            => $school,
				tourn             => $tourn,
				tourn_settings    => $tourn_settings,
				categories        => \@categories,
				category_settings => \%category_settings,
				events            => \@events,
				concession_array  => \@concessions,
				event_settings    => \%event_settings,
				all               => 1,
				bulk              => 1,
				entries           => $school_entries{$school->id},
				judges            => $school_judges{$school->id},
				fines             => $school_fines{$school->id},
				hires             => $school_hires{$school->id}
			);

		next if $total == -0;

		$m->print('"'.$school->name.'",');
		$m->print('"'.sprintf("%.2f", $total ).'",');
		$m->print('"'.$school->setting("contact_name").'",');
		$m->print('"'.$school->setting("contact_number").'",');
		$m->print('"'.$school->setting("contact_email").'",');
		$m->print('"'.$school->setting("refund_payable").'",');
		$m->print('"'.$school->setting("refund_address")."\"\n");


	} # end of foreach school

	$m->abort;

</%init>

