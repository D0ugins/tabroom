<%args>
	$person
	$tourn
	$event_id
</%args>
<%init>

	my $event = Tab::Event->retrieve($event_id);
	my @rounds = $event->rounds( type => "prelim");

	my @ballots = $m->comp('/funclib/event_ballots.mas', event => $event);
	my @panels = $m->comp('/funclib/event_panels.mas', event => $event);

	my %panel_entries;
	my %entry_ballot;

	my %panel_round = map {$_->id => $_->round->id} @panels;
	my %speaker_order = map { $_->entry->id."-".$_->panel->id => $_->speakerorder} @ballots;

	foreach my $ballot (@ballots) {
		push @{$entry_ballot{$ballot->entry->id}{$ballot->panel->id}}, $ballot;
	}

	my %done;

	Tab::Panel->set_sql( order_swap => "
		update ballot set speakerorder = ? where panel = ? and speakerorder = ? 
	");

	foreach my $entry ($event->entries( active => 1)) {

		next if $done{$entry}++;

		my ($status, $early, $mid, $late) = 
			$m->comp("/funclib/ncfl_speakers.mas", entry => $entry);

		my @needs;
		my @panels = $m->comp("/funclib/entry_panels.mas", entry => $entry);

		foreach my $panel (@panels) {
			push @{$panel_entries{$panel->id}}, $entry;
		}

		next if $status eq "OK";

		push (@needs, "early") unless $early;
		push (@needs, "mid") unless $mid;
		push (@needs, "late") unless $late;

		NEED:
		foreach my $need (@needs) {

			foreach my $panel (@panels) {

				next unless $panel->round->type eq "prelim";
				my $order = $speaker_order{$entry->id."-".$panel->id};

				# Don't move the entry if it would just create other problems
				next if ($early == 1) && $order < 3;
				next if ($mid == 1)  && $order > 2 && $order < 6;
				next if ($late == 1) && $order > 5;

				my ($other, $other_order) = &find_other($entry,$panel,$need);

				if ($other) {

					#OBS	
					Tab::Panel->sql_order_swap->execute($order, $panel->id, 210);
					Tab::Panel->sql_order_swap->execute(210, $panel->id, $other_order);

					#EBS
					Tab::Panel->sql_order_swap->execute($other_order, $panel->id, 310);
					Tab::Panel->sql_order_swap->execute(310, $panel->id, $order);

					next NEED;
				}
			}
		}
	}

	foreach my $panel (@panels) {
		next unless $panel->round->type eq "prelim";

		my $panel_id = $panel->id;
		my @entries;

		if ($panel_entries{$panel->id}) { 
			@entries = 
			sort {$speaker_order{$a->id."-".$panel_id} <=> $speaker_order{$b->id."-".$panel_id}} 
			@{$panel_entries{$panel_id}};
		}

		my $order;

		foreach my $entry (@entries) {
			$order++;
			foreach my $ballot (@{$entry_ballot{$entry->id}{$panel_id}}) {
				$ballot->speakerorder($order);
				$ballot->update;
			}
		}

	}

	$m->redirect("speaker_order.mhtml?event_id=$event_id");

	sub find_other {

		my ($entry, $panel, $need) = @_;

		my %others_speaks = map {$_->entry->id => $_->speakerorder} $panel->ballots;

		OTHER:
		foreach my $other (keys %others_speaks) {

			next if $other == $entry->id;

			my $other_order = $others_speaks{$other};

			if ($need eq "early") {
				next OTHER if $other_order > 2;
				my ($other_status, $other_early, $other_mid, $other_late) = $m->comp("/funclib/ncfl_speakers.mas", entry_id => $other);
				next OTHER if $other_early < 2;
			}

			if ($need eq "mid") {
				next OTHER if $other_order < 2 || $other_order > 5;
				my ($other_status, $other_early, $other_mid, $other_late) = $m->comp("/funclib/ncfl_speakers.mas", entry_id => $other);
				next OTHER if $other_mid < 2;
			}

			if ($need eq "late") {
				next OTHER if $other_order < 6;
				my ($other_status, $other_early, $other_mid, $other_late) = $m->comp("/funclib/ncfl_speakers.mas", entry_id => $other);
				next OTHER if $other_late < 2;
			}

			# If we got this far without getting bumped, then we have found
			# the candidate with which the current one should switch.

			return ($other, $other_order);

		}

	}

</%init>
