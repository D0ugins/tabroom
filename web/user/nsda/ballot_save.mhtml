<%args>
	$tourn_id => undef
	$person
	$person_settings
	$perms
</%args>
<%init>

	my $tourn = Tab::Tourn->retrieve($tourn_id);

	unless ($tourn) {
		$m->comp("/funclib/abort.mas",
			message => "No tournament found with ID $tourn_id"
		);
	}

	my $district_id = $tourn->setting("nsda_district");

	unless ($district_id) {
		$m->comp("/funclib/abort.mas",
			message => "Tournament is not marked as a district tournament"
		);
	}

	my $district = Tab::District->retrieve($district_id);

	unless ($district) {
		$m->comp("/funclib/abort.mas",
			message => "No district was found for ID $district_id"
		);
	}

	unless ($person->site_admin || $person_settings->{"nsda_admin"} || $perms->{"district"}{$district->id} eq "chair") {
		$m->comp("/funclib/abort.mas",
			message => "Only the district chair may make that change"
		);
	}

	foreach my $event ($tourn->events(type => "debate")) {

		unless ($event->setting("nsda_ballot_order") == $ARGS{$event->id."_ballots"}) {

			$event->setting("nsda_ballot_order", $ARGS{$event->id."_ballots"});

			$m->comp("/funclib/log.mas",
				tourn       => $tourn->id,
				type        => "districts",
				person      => $person->id,
				description => "Ballot order for ".$event->abbr." changed to ".$ARGS{$event->id."_ballots"}
			);
		}
	}

	my $address;
	foreach my $field ("street", "street2", "name", "city", "state", "zip", "country") {
		$address->{$field} = $ARGS{"address_".$field};
	}

	$tourn->setting("shipping_address", "json", $address);

	my $msg = "District tournament order information saved.";
	$m->redirect("district.mhtml?district_id=$district_id&default=orders&msg=$msg");

</%init>
