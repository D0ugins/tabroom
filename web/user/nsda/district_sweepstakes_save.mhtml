<%args>
	$tourn_id => undef
	$return   => undef
</%args>
<%init>

	my $tourn = Tab::Tourn->retrieve($tourn_id) if $tourn_id;
	$m->abort unless $tourn;

	my %types; 

	foreach my $event ($tourn->events) {
		push @{$types{$event->type}}, $event;
	}

	my %components;

	my %numbers = (
		debate   => 6,
		speech   => 8,
		congress => 5
	);

	foreach my $type (keys %types) { 

		my $sweep_set = Tab::SweepSet->create({
			tourn => $tourn->id,
			name  => ucfirst($type)
		});

		$sweep_set->rule("entries", $numbers{$type});

		Tab::SweepRule->create({
			tag       => "nsda_place",
			count     => "all",
			place     => 0,
			sweep_set => $sweep_set->id
		});

		$components{$type} = Tab::SweepSet->create({
			tourn => $tourn->id,
			name  => ucfirst($type)." Component"
		}) unless $type eq "congress";

		$components{$type}->rule("entries", 5) 
			if $components{$type};

		foreach my $event (@{$types{$type}}) { 
			Tab::SweepEvent->create({
				sweep_set => $sweep_set->id,
				event     => $event->id
			});

			if ($components{$type}) { 
				Tab::SweepEvent->create({
					sweep_set => $components{$type}->id,
					event     => $event->id
				});
			}
		}
	}

	if ($components{'debate'} && $components{"speech"}) { 

		my $set = Tab::SweepSet->create({
			tourn => $tourn->id,
			name  => "Yearly Sweeps"
		});

		Tab::SweepInclude->create({
			parent => $set->id,
			child => $components{'debate'}->id
		});

		Tab::SweepInclude->create({
			parent => $set->id,
			child => $components{'speech'}->id
		});

	}

	my $cumulative = Tab::SweepSet->create({
		tourn => $tourn->id,
		name  => "Cumulative Award"
	});

	Tab::SweepRule->create({
		tag       => "points_per",
		count     => "all",
		value     => 1,
		place     => 0,
		sweep_set => $cumulative->id
	});

	foreach my $type (keys %types) { 

		foreach my $event (@{$types{$type}}) { 
			Tab::SweepEvent->create({
				sweep_set => $cumulative->id,
				event     => $event->id
			});
		}
	}

	if ($return) { 

		return;

	} else { 

		my $msg = "Sweeps added to ".$tourn->name;

		$m->redirect("/user/admin/nsda/tourns.mhtml?msg=$msg");

	}

</%init>
