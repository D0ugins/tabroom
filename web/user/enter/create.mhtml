<%args>
	$person
	$session
	$tourn_id
	$chapter_id
	$return => undef
</%args>
<%init>

	my $tourn = Tab::Tourn->retrieve($tourn_id);
	my %tourn_settings = $tourn->all_settings;

	my $chapter = Tab::Chapter->retrieve($chapter_id);

	$m->comp("/user/chapter/auth.mas", 
		chapter => $chapter,
		person  => $person,
		session => $session) 
	unless $return;

	if ($tourn_settings{"nsda_members_only"}) { 


	} elsif ($tourn_settings{"nsda_md_members_only"}) { 

		unless ($chapter->nsda > 0) { 
			my $err = "Only NSDA Member Schools may register for nationals. <br />";
			$err .= "To confirm your NSDA membership, Link your Tabroom school to the NSDA below";
			$m->redirect("/user/chapter/nsda_middle.mhtml?chapter_id=".$chapter->id."&err=$err&join=1");
		}

		unless ($chapter->setting("nsda_paid") > 0) { 

			my $err = "To register for nationals you must first pay your annual membership dues. <br />";
			$m->redirect("/user/chapter/nsda_middle.mhtml?chapter_id=".$chapter->id."&err=$err&pay=1");
		}

	}

	my ($existing_school) = $chapter->schools( tourn => $tourn->id);

	if ($existing_school) {
		$m->redirect("/user/enter/entry.mhtml?school_id=".$existing_school->id);
	}

	my $now = DateTime->now();

	$m->abort if $tourn->reg_start > $now;
	$m->abort if $tourn->reg_end < $now;

	my ($school_code, $school_region) = $m->comp(
		"/funclib/chapter_code.mas", 
			tourn   => $tourn,
			chapter => $chapter
		);

	my $school = Tab::School->create({
		tourn          => $tourn->id,
		chapter        => $chapter->id,
		name           => $chapter->name,
		code           => $school_code,
		region         => $school_region
	});

	$school->setting("entered_on", "date", $now);
	$school->setting("contact", $person->id);
	$school->setting("contact_name", $person->first." ".$person->last);
	$school->setting("contact_number", $person->phone);
	$school->setting("contact_email", $person->email);

	$m->comp('/funclib/chapter_conflicts.mas', school => $school);

	if ($tourn_settings{"track_reg_changes"}) {

		my $description = "School registered by ".$person->first." ".$person->last;

		my $change = Tab::ChangeLog->create({
			tourn       => $tourn->id,
			school      => $school->id,
			type        => "registration",
			description => $description,
			person      => $person->id
		});

	}

	foreach my $fine ($tourn->tourn_fees) {

		next unless $fine->amount > 0;
		next if $fine->start && $fine->start > $now;
		next if $fine->end && $fine->end < $now;

		my $fee = Tab::Fine->create({
			school    => $school->id,
			amount    => $fine->amount,
			reason    => $fine->reason,
			tourn     => $tourn->id,
			levied_at => $now,
			payment => 0,
			deleted => 0
		});

	}

	if ($tourn_settings{"nsda_district"}) {

	}

	return $school if $return;

	$m->redirect("disclaimer.mhtml?school_id=".$school->id) if $tourn_settings{"disclaimer"};

	$m->redirect("entry.mhtml?school_id=".$school->id);

</%init>
