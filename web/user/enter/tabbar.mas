<%args>
	$school
	$whoami => undef
</%args>
<%init> 

	my $tourn = $school->tourn;

	my %tourn_settings = $tourn->all_settings;

	my $tz = $tourn->tz;
	$tz = "UTC" unless $tz;
	my $now = DateTime->now(time_zone => $tz);

	my $short;

	foreach my $category ($school->tourn->categories) { 

		my %category_settings = $category->all_settings();

		my ($uncovered, $overage) = $m->comp(
			"/funclib/judgemath/uncovered_burden_by_category.mas", 
			category          => $category,
			category_settings => \%category_settings,
			school            => $school
		); 

		$short++ if $uncovered;

		unless ($short) { 

			my %stimes_under = $m->comp(
				"/funclib/judgemath/judge_partials_short.mas", 
					category          => $category,
					category_settings => \%category_settings,
					school            => $school
			);

			foreach my $key (keys %stimes_under) {
				next unless $stimes_under{$key} > 0;
				$short++;
				last if $short;
			}

		}

		last if $short;

	}	

	my $ratings;

	foreach my $category ($school->tourn->categories) { 

		my $strike_start = $category->setting("strike_start");

		if ($strike_start) { 
			$ratings++ if ($strike_start->epoch < $now->epoch);
		}

		last if $ratings;
	}	

	my $nsda_district = $tourn_settings{"nsda_district"};

	my $element = "h3";
	$element = "h4" if length($tourn->name) > 28;
	$element = "h5" if length($tourn->name) > 40;

	Tab::Event->set_sql(legislation_events => "
		select event.*
		from event, event_setting
		where event.tourn = ? 
		and event.id = event_setting.event
		and event_setting.tag = 'upload_legislation'
		and exists (
			select entry.id
			from entry
			where entry.school = ? 
			and entry.event = event.id
		)"
	);

	my @legislation = Tab::Event->search_legislation_events($tourn->id, $school->id);


</%init>

	<div class="full nospace">

		<span class="threequarters">
			<<% $element %> class="nospace">
				<% $tourn->name %>
			<<% '/'.$element %>>
		</span>

		<span class="quarter rightalign nowrap">
			<h6>
				<% $school->short_name %>
				<% $school->code ? "<br />(".$school->code.")" : "" %>
			</h6>
		</span>

	</div>

	<ul id="tabnav">

		<li class="<% ($whoami eq "tourn") ? "selected" : "" %>">
			<a href="/user/enter/entry.mhtml?school_id=<% $school->id %>">General</a>
		</li>

%		if ($tourn_settings{"onsite_registration"}) { 

<%perl>

			my $onsite_starts = $tourn_settings{"onsite_starts"};
			$onsite_starts->set_time_zone($tz) if $onsite_starts;

			my $onsite_ends = $tourn_settings{"onsite_ends"};
			$onsite_ends->set_time_zone($tz) if $onsite_ends;


			if ($onsite_starts < $now && $now < $onsite_ends) { 

</%perl>
				<li 
					class="<% ($whoami eq "onsite") ? "selected" : "" %>
							<% $school->onsite ? "" : "warning" %>">

					<a href="/user/enter/onsite.mhtml?school_id=<% $school->id %>">
						Onsite Confirmation
					</a>

				</li>

%			}

%		}

		<li class="<% ($whoami eq "by_event") ? "selected" : "" %>">
			<a 
				href="/user/enter/students.mhtml?school_id=<% $school->id %>"
				>Entry<% $nsda_district || $tourn_settings{"nsda_nats"} ? "" : " (by Event)" %></a>
		</li>

%		unless ($nsda_district || $tourn_settings{"nsda_nats"} || $tourn_settings{"nsda_ms_nats"}) { 

			<li class="<% ($whoami eq "by_person") ? "selected" : "" %>">
				<a href="/user/enter/by_person.mhtml?school_id=<% $school->id %>">Entries (by Person)</a>
			</li>

%		} elsif ($nsda_district) { 

			<li class="<% ($whoami eq "nsda") ? "selected" : "" %>">
				<a href="/user/enter/nsda.mhtml?school_id=<% $school->id %>">NSDA Forms</a>
			</li>
%		}

		<li class="<% ($short) ? "warning" : ($whoami eq "judges") ? "selected" : "" %>">
			<a href="/user/enter/judges.mhtml?school_id=<% $school->id %>">Judges</a>
		</li>

%		if ($tourn_settings{"housing"}) { 
			<li class="<% ($whoami eq "housing") ? "selected" : "" %>">
				<a href="/user/enter/housing.mhtml?school_id=<% $school->id %>">Housing</a>
			</li>
%		}

%		if ($tourn->concessions) {

			<li class="<% ($whoami eq "concessions") ? "selected" : "" %>">

				<a href="/user/enter/concessions.mhtml?school_id=<% $school->id %>">
					<% $tourn_settings{"concession_name"}
						? $tourn_settings{"concession_name"} 
						: "Concessions" %>
				</a>

			</li>

%		}

%		if (@legislation) {

			<li class="<% ($whoami eq "legislation") ? "selected" : "" %>">

				<a href="/user/enter/legislation.mhtml?school_id=<% $school->id %>">
					Legislation
				</a>

			</li>
	

%		}


<%perl>

	if ($ratings) { 

		my $red_ratings;

		foreach my $other_category (sort {$a->name cmp $b->name} $tourn->categories) {

			my @entries= $m->comp(
				"/funclib/category_entries.mas", 
				category  => $other_category,
				school    => $school,
				preffable => 1
			);
			next unless @entries;

			my $pref_style = $other_category->setting("prefs");
			next unless $pref_style;
			next if $pref_style eq "community";

			foreach my $entry (@entries) { 
				my @unrated = $m->comp("/funclib/entry_unrated.mas", entry => $entry);
				$red_ratings++ if @unrated;
				last if $red_ratings;
			}

			last if $red_ratings;
		}

</%perl>

			<li class="<% $red_ratings ? "warning" : "" %> 
				<% ($whoami eq "ratings" || $whoami eq "conflicts") ? "selected" : "" %>"
			>
				<a href="/user/enter/ratings/index.mhtml?school_id=<% $school->id %>">Prefs</a>
			</li>
%		}

		<li>
			<a href="/index/tourn/index.mhtml?tourn_id=<% $tourn->id %>">Website</a>
		</li>

	</ul>
