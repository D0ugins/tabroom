<%args>
	$person
	$session
	$defaults
	$first      => undef
	$middle     => undef
	$last       => undef
	$email      => undef
	$phone      => undef
	$provider   => undef
	$street     => undef
	$city       => undef
	$state      => undef
	$country    => undef
	$no_email   => 0
	$zip        => undef
	$postal     => undef
	$pronoun    => undef
	$timezone   => undef
	$please_stop_screaming => 0
</%args>
<%init>

	$m->abort unless $person;
	$m->abort unless $first;
	$m->abort unless $last;
	$m->abort unless $email;

	$email =~ s/\s+//g;
	$email = lc($email);

	my $token = $defaults->{"token"};

	unless ($token) {

		my $err = "No authentication token sent; please use only the profile form on Tabroom.";
		$m->redirect("/user/login/profile.mhtml?err=$err");

	} elsif ($token ne $ARGS{"token"}) {

		my $err = "Authentication token mismatch; please use only the profile form on Tabroom.";
		$m->redirect("/user/login/profile.mhtml?err=$err");

	}

	my $diff;

	unless ($email eq $person->email) {

		if ($email eq lc($person->email)) {

			$person->email(lc($email));
			$person->update();

		} else {

			my @existing_emails = Tab::Person->search(
				email => $email
			);

			if (@existing_emails) {
				my $err = "Email address $email already has an account: ".$existing_emails[0]->first." ".$existing_emails[0]->last;
				$m->redirect("/user/login/profile.mhtml?err=$err");
			}
		}

		foreach my $login ($person->logins(username => $person->email)) {
			$login->username(lc($email));
		}

		$diff++;

	}

	$phone =~ s/[\D_]//g;

	# Make sure the email is actually an email
	unless (Email::Valid->address( -address => $email, -mxcheck => 0 )) {
		my $err = "$email is not a validly formatted email";
		$m->redirect("/user/login/profile.mhtml?err=$err");
	}


	$person->first($first);
	$person->middle($middle);
	$person->last($last);
	$person->street($street);
	$person->city($city);
	$person->email($email);
	$person->state($state);
	$person->country($country);
	$person->no_email($no_email);
	$person->zip($zip);
	$person->postal($postal);
	$person->pronoun($pronoun);
	$person->phone($phone);
	$person->provider($provider);
	$person->tz($timezone);
	$person->update();

	my $ldap;

	# Refresh the LDAP server
	foreach my $login ($person->logins) {
		$ldap .= $m->comp("/funclib/ldap_person.mas", login => $login);
	}

	$person->setting("please_stop_screaming", $please_stop_screaming);

	delete $defaults->{"token"};
	$session->default($defaults);

	Tab::log("PROFILE CHANGE: Account ID ".$person->id." ($email) was modified from IP address ".$ENV{REMOTE_ADDR});
	Tab::log("LDAP Update yielded $ldap");

	my $msg = "Changes saved";

	if ($diff) {
		$m->comp("/funclib/email_confirm.mas",
			person => $person,
			send   => 1
		);
	}

	$m->redirect("/user/login/profile.mhtml?msg=$msg");

</%init>
