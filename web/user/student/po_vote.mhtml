<%args>
	$person
	$entry_id
	$panel_id
</%args>
<%init>

	my $entry = Tab::Entry->retrieve($entry_id);
	my $panel = Tab::Panel->retrieve($panel_id);

	unless ($entry) {
		$m->comp("/funclib/abort.mas", warning => "No entry found for ID $entry_id");
	}
	unless ($panel) {
		$m->comp("/funclib/abort.mas", warning => "No chamber found for ID $panel_id");
	}

	my @panel_entries = $m->comp("/funclib/panel_entries.mas", panel => $panel);

	my $ok;

	foreach my $pe (@panel_entries) {
		if ($pe->id == $entry->id) {
			$ok++;
			last;
		}
	}

	unless ($ok) {
		$m->comp("/funclib/abort.mas", warning => "That entry is not in this chamber");
	}

	undef $ok;

	foreach my $student ($entry->students) {
		$ok++ if $student->person == $person;
	}

	unless ($ok) {
		$m->comp("/funclib/abort.mas", warning => "Your tabroom account is not linked to a competitor in that entry");
	}

	my $round = $panel->round;
	my $event = $round->event;

	my $target_id = $event->setting("po_contest");

	unless ($target_id) {
		$m->comp("/funclib/abort.mas", warning => "No PO contest is enabled for your event");
	}

	my $target_event = Tab::Event->retrieve($target_id);

	unless ($target_event) {
		$m->comp("/funclib/abort.mas", warning => "No PO event was found");
	}

	my $target_round = $target_event->rounds( name => $round->name )->first;

	unless ($target_round) {
		$m->comp("/funclib/abort.mas", warning => "No session ".$round->name." was found in the PO event");
	}

	my $target_panel = $target_round->panels( letter => $panel->letter )->first;

	unless ($target_panel) {
		$m->comp("/funclib/abort.mas", warning => "No chamber ".$panel->letter." was found in this session");
	}

	my $student_vote = Tab::StudentVote->search(
		panel => $target_panel->id,
		voter => $entry->id,
		tag   => "winloss",
		value => 1
	)->first;

	if ($student_vote) {
		$m->comp("/funclib/abort.mas",
			warning => "You have already completed your ballot for this session.  You voted for ".$student_vote->entry->name.".");
	}

	my @target_entries = $m->comp("/funclib/panel_entries.mas", panel => $target_panel);

	if ($ARGS{"winner"}) {

		my $now = DateTime->now();

		my $we;

		foreach my $te (@target_entries) {

			my $winner = 0;

			if ($te == $ARGS{"winner"}) {
				$winner = 1;
				$we = $te;
			}

			my $student_vote = Tab::StudentVote->create({
				panel      => $target_panel->id,
				entry      => $te->id,
				voter      => $entry->id,
				tag        => "winloss",
				value      => $winner,
				entered_by => $person,
				entered_at => $now,
			});
		}

		my $msg = "You have voted for ".$we->code." ".$we->name." succesfully.";
		$m->redirect("/user/student/index.mhtml?msg=$msg");
	}

</%init>

	<& "/user/menu.mas",
		whoami => "competitor",
		person => $person,
	&>

	<div class="main">

		<span class="half">
			<h4><% $target_event->name  %></h4>
		</span>

		<span class="half rightalign">
			<h5>Competitor Vote</h5>
		</span>

		<h6 class="semibold bluetext centeralign padvertmore marvertmore">
			Chamber <% $panel->letter %> <% $round->realname %>
		</h6>

		<p class="semibold redtext centeralign bigger">
			Once you cast your vote, you cannot change it.  Please, be certain.
		</p>

		<form action="po_vote.mhtml" method="post">

		<input
			type  = "hidden"
			name  = "panel_id"
			value = "<% $panel_id %>"
		>

		<input
			type  = "hidden"
			name  = "entry_id"
			value = "<% $entry_id %>"
		>

		<div class="yellowrow semibold">
			<span class="third">
				Code
			</span>
			<span class="third">
				Name
			</span>
			<span class="third">
				Vote (pick one)
			</span>
		</div>

%		foreach my $te (@target_entries) {
			<div class="row">
				<span class="third">
					<% $te->code %>
				</span>
				<span class="third">
					<% $te->name %>
				</span>
				<span class="third">
					<span class="spacer"></span>
					<span class="spacer"></span>
					<input
						type  = "radio"
						name  = "winner"
						value = "<% $te->id %>"
					>
				</span>
			</div>
%		}

		<div class="liblrow rightalign">
			<span class="third centeralign">
				<input
					type  = "submit"
					value = "Submit Vote"
				>
			</span>
		</div>

		</form>

	</div>
