<%args>
	$person
	$judge_id
	$panel_id
</%args>
<%init>

	my $judge  = Tab::Judge->retrieve($judge_id) if $judge_id;
	my $panel  = Tab::Panel->retrieve($panel_id) if $panel_id;

	unless ($judge && $panel) {
		$m->comp("/funclib/abort.mas",
			message => "Could not find a section for $panel_id or judge for $judge_id"
		);
	}

	unless ($judge->person && $judge->person->id == $person->id) {
		$m->comp("/funclib/abort.mas",
			message => "Your account does not have access to vote for that judge"
		);
	}

	my @ballots =
		sort {$a->speakerorder <=> $b->speakerorder}
		$judge->ballots(panel => $panel->id);

	unless (@ballots) {
		$m->comp("/funclib/abort.mas",
			message => "Could not find a ballot for ".$judge->last." in that section"
		);
	}

	foreach my $ballot (@ballots) {

		my $comments = $ballot->scores(
			tag => "comments"
		)->first;

		my $text;

		if ($ARGS{$ballot->id}) {
			$text = $m->comp("/funclib/save_editor.mas", text => $ARGS{$ballot->id});
		}

		if ($text) {

			unless ($comments) {
				$comments = Tab::Score->create({
					tag      => "comments",
					ballot   => $ballot->id,
					student  => 0
				});
			}

			$comments->text($text);
			$comments->update();

		} elsif ($comments) {

			$comments->delete();
		}
	}

	my $msg = "Comments and feedback saved";

	$m->redirect("/user/judge/panels.mhtml?msg=$msg");

</%init>
